
Процедура ОбработкаПроведения(Отказ, Режим)
	
	//  1. Получение запросом данных документа
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РезервированиеТоваровТовары.Номенклатура КАК Номенклатура,
	|	СУММА(РезервированиеТоваровТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.РезервированиеТоваров.Товары КАК РезервированиеТоваровТовары
	|ГДЕ
	|	РезервированиеТоваровТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РезервированиеТоваровТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Количество КАК Количество
	|ИЗ
	|	Товары КАК Товары";     
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаТовары = РезультатЗапроса.Выбрать();
	
	//  2. Формирование движений-приход регистра
	Движения.ТоварыВРезерве.Очистить();
	
	Пока ВыборкаТовары.Следующий() Цикл
		
		Движение = Движения.ТоварыВРезерве.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаТовары.Номенклатура;
		Движение.Склад = Склад;
		Движение.Количество = ВыборкаТовары.Количество;
		Движение.ДокументРезерва = ДокументОснование;
		
	КонецЦикла;
	
	//  3. Запись движений в БД
	Движения.ТоварыВРезерве.Записать();
	
	//  4. Запрос, получающий отрицательные остатки из регистра
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрНакопления.Запасы.Остатки(
	|			&МоментВремени,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						Товары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						Товары КАК Товары)
	|				И Склад = &Склад) КАК ЗапасыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыВРезервеОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыВРезервеОстатки.КоличествоОстаток КАК КоличествоРезерв
	|ПОМЕСТИТЬ втРезервы
	|ИЗ
	|	РегистрНакопления.ТоварыВРезерве.Остатки(
	|			&МоментВремени,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						Товары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						Товары КАК Товары)
	|				И Склад = &Склад) КАК ТоварыВРезервеОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Товары.Номенклатура) КАК НоменклатураПредставление,
	|	Товары.Количество КАК Количество,
	|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(втРезервы.КоличествоРезерв, 0) - Товары.Количество КАК КоличествоРезерв
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|		ПО Товары.Номенклатура = втОстатки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРезервы КАК втРезервы
	|		ПО Товары.Номенклатура = втРезервы.Номенклатура
	|ГДЕ
	|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(втРезервы.КоличествоРезерв, 0) < 0";         
	ГраницаКонтроля = Новый Граница(МоментВремени(), ВидГраницы.Включая);
	Запрос.УстановитьПараметр("МоментВремени", ГраницаКонтроля);
	Запрос.УстановитьПараметр("Склад", Склад);
	РезультатЗапроса = Запрос.Выполнить();
	
	//  5. Вывод сообщений о недостатке товаров
	Если Не РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		ВыборкаОшибки = РезультатЗапроса.Выбрать();
		Пока ВыборкаОшибки.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Товара " + ВыборкаОшибки.НоменклатураПредставление + " недостаточно на остатках. Резервируется: "
			+ ВыборкаОшибки.Количество + ", остаток: " + ВыборкаОшибки.КоличествоОстаток + ", резерв: " + ВыборкаОшибки.КоличествоРезерв;
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда 
		
		Склад = ДанныеЗаполнения.Склад;
		Автор = ПараметрыСеанса.ТекущийПользователь;
		ДокументОснование = ДанныеЗаполнения;
		
		//Товары
		Для Каждого Товар Из ДанныеЗаполнения.Товары Цикл 
			
			нТовар = Товары.Добавить();
			нТовар.Номенклатура = Товар.Номенклатура;
			нТовар.ЕдиницаИзмерения = Товар.ЕдиницаИзмерения;
			нТовар.Количество = Товар.Количество;
			
		КонецЦикла;
		
		//Упаковка
		Для Каждого Упаковка Из ДанныеЗаполнения.Упаковка Цикл 
			
			нТовар = Товары.Добавить();
			нТовар.Номенклатура = Упаковка.Номенклатура;
			нТовар.ЕдиницаИзмерения = Упаковка.ЕдиницаИзмерения;
			нТовар.Количество = Упаковка.Количество;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
