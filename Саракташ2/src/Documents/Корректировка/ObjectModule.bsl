
Процедура ОбработкаПроведения(Отказ, Режим)
	
	//Свернуть ТЧ
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тчТовары.Склад КАК Склад,
	|	тчТовары.Номенклатура КАК Номенклатура,
	|	тчТовары.Количество КАК Количество
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&тчТовары КАК тчТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТовары.Склад КАК Склад,
	|	втТовары.Номенклатура КАК Номенклатура,
	|	СУММА(втТовары.Количество) КАК Количество
	|ИЗ
	|	втТовары КАК втТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	втТовары.Склад,
	|	втТовары.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(втТовары.Количество) <> 0";
	
	Запрос.УстановитьПараметр("тчТовары", Товары.Выгрузить(,"Склад, Номенклатура, Количество"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Движения.Запасы.Записывать = Истина;
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.Запасы.Добавить();
		
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Склад = Выборка.Склад;
		
		Если Выборка.Количество > 0 Тогда
			
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Количество = Выборка.Количество;
			
		Иначе
			
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Количество = Выборка.Количество * - 1;
			
		КонецЕсли;		
		
	КонецЦикла;
	
КонецПроцедуры
