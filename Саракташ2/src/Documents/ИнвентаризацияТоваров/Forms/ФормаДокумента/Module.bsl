&НаСервере
Процедура ЗаполнитьОстаткиНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тзТовары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&тзТовары КАК тзТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.Запасы.Остатки(
	|			&Дата,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						втТовары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						втТовары КАК втТовары)
	|				И Склад = &Склад) КАК ЗапасыОстатки";
	Запрос.УстановитьПараметр("тзТовары", Объект.Товары.Выгрузить(,"Номенклатура"));
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		мсСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура", Выборка.Номенклатура));
		
		Для Каждого нСтрока Из мсСтроки Цикл
			
			нСтрока.Остаток = Выборка.КоличествоОстаток;
			
		КонецЦикла;		
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОстатки(Команда)
	
	Если Параметры.Ключ.Пустая() Тогда
		Предупреждение("Необходимо сначала записать документ!");
		Возврат;
	КонецЕсли;
	
	Если Вопрос("Перезаполнить остатки товаров?", РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьОстаткиНаСервере();
	
	Сообщить("Остатки перезаполнены", СтатусСообщения.Информация);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыНаСервере()	
	
	Объект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Запасы.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ втАссортимент
	|ИЗ
	|	РегистрНакопления.Запасы КАК Запасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрНакопления.Запасы.Остатки(&Дата, Склад = &Склад) КАК ЗапасыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАссортимент.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,	
	|	втАссортимент.Номенклатура.Артикул КАК Артикул
	|ИЗ
	|	втАссортимент КАК втАссортимент
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|		ПО втАссортимент.Номенклатура = втОстатки.Номенклатура";
	
	Запрос.УстановитьПараметр("Дата", Новый МоментВремени(Объект.Дата, Объект.Ссылка));
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		нСтрока = Объект.Товары.Добавить();
		нСтрока.Номенклатура = Выборка.Номенклатура;
		нСтрока.Остаток = Выборка.КоличествоОстаток;
		нСтрока.Артикул = Выборка.Артикул;		
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТовары(Команда)
	
	Если Параметры.Ключ.Пустая() Тогда
		Предупреждение("Необходимо сначала записать документ!");
		Возврат;
	КонецЕсли;

	
	Если Вопрос("Перезаполнить товары?", РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТоварыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДинамическиеКолонки()
	
	Для Каждого Строка Из Объект.Товары Цикл 
		
		Строка.Артикул = Строка.Номенклатура.Артикул;		
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ЗаполнитьДинамическиеКолонки();
КонецПроцедуры
 
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ЗаполнитьДинамическиеКолонки();
КонецПроцедуры
 
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Параметры.Ключ.Пустая() Тогда
		Объект.Статус = "Новый";		
	КонецЕсли;
	
	ЗаполнитьДинамическиеКолонки();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоФактПриИзменении(Элемент)
	Элементы.Товары.ТекущиеДанные.Отклонение = Элементы.Товары.ТекущиеДанные.КоличествоФакт - Элементы.Товары.ТекущиеДанные.Остаток;
КонецПроцедуры

