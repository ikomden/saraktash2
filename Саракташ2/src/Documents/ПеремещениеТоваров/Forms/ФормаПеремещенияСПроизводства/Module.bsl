
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка) Тогда
		Для Каждого СтрокаТоваров Из НовыеТовары Цикл
			Если Не СтрокаТоваров.Отметка Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ТекущийОбъект.Товары.Добавить();
			НоваяСтрока.Номенклатура = СтрокаТоваров.Номенклатура;
			НоваяСтрока.Количество = 1;
			НоваяСтрока.СерийныйНомер = СтрокаТоваров.СерийныйНомер;
			НоваяСтрока.ВязальщицаНаСайте = СтрокаТоваров.МастерицаДляПроверки;
		КонецЦикла;
	Иначе
		СкладБрак = Константы.СкладБрак.Получить(); 
		Для Каждого СтрокаТоваров Из Объект.Товары Цикл
			ТекущийОбъект.Товары[СтрокаТоваров.НомерСтроки - 1].СкладПолучатель = 
			?(СтрокаТоваров.Брак, СкладБрак, Справочники.Склады.ПустаяСсылка());
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ДокументОснование.Видимость = ЗначениеЗаполнено(Объект.ДокументОснование);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.СтраницыТоваров.ТекущаяСтраница = Элементы.СтраницаНовыеТовары;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗапасыПоСерийнымНомерамОстатки.СерийныйНомер КАК СерийныйНомер,
		|	ЗапасыПоСерийнымНомерамОстатки.КоличествоОстаток КАК Количество,
		|	ЗапасыПоСерийнымНомерамОстатки.СерийныйНомер.Владелец КАК Номенклатура,
		|	ДанныеСерийныхНомеров.Бирка КАК Бирка,
		|	ДанныеСерийныхНомеров.ВязальщицаНаСайте КАК МастерицаДляПроверки
		|ИЗ
		|	РегистрНакопления.ЗапасыПоСерийнымНомерам.Остатки(, Склад = &СкладПроизводства) КАК ЗапасыПоСерийнымНомерамОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеСерийныхНомеров КАК ДанныеСерийныхНомеров
		|		ПО ЗапасыПоСерийнымНомерамОстатки.СерийныйНомер = ДанныеСерийныхНомеров.СерийныйНомер";
		Запрос.УстановитьПараметр("СкладПроизводства", Справочники.Склады.Саракташ);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = НовыеТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
	Иначе
		Элементы.СтраницыТоваров.ТекущаяСтраница = Элементы.СтраницаТовары;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДопПоля()
	
	СкладБрак = Константы.СкладБрак.Получить();
	
	Номера = Объект.Товары.Выгрузить(, "СерийныйНомер").ВыгрузитьКолонку("СерийныйНомер");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеСерийныхНомеров.СерийныйНомер КАК СерийныйНомер,
	|	ДанныеСерийныхНомеров.Бирка КАК Бирка
	|ИЗ
	|	РегистрСведений.ДанныеСерийныхНомеров КАК ДанныеСерийныхНомеров
	|ГДЕ
	|	ДанныеСерийныхНомеров.СерийныйНомер В (&Номера)";
	Запрос.УстановитьПараметр("Номера", Номера);
	Бирки = Новый Соответствие;
	Для Каждого Стр Из Запрос.Выполнить().Выгрузить() Цикл
		Бирки.Вставить(Стр.СерийныйНомер, Стр.Бирка);		
	КонецЦикла;
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		СтрокаТоваров.Брак = СтрокаТоваров.СкладПолучатель = СкладБрак;
		СтрокаТоваров.Бирка = Бирки[СтрокаТоваров.СерийныйНомер];
	КонецЦикла;   		
	
КонецПроцедуры


&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект) 	
	ПрочитатьДопПоля();  	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ПрочитатьДопПоля();
КонецПроцедуры
