
&НаКлиенте
Процедура СохранитьВФайл(Команда)
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Оповещение = Новый ОписаниеОповещения("ВыборФайлаПродолжение",ЭтаФорма,Новый Структура("Период",ТекДанные.Период));
	Диалог.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаПродолжение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Текст = ТекстСообщения(ДополнительныеПараметры.Период);
	Если Текст = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьТекста = Новый ЗаписьТекста(ВыбранныеФайлы[0], КодировкаТекста.UTF8);
	ЗаписьТекста.Записать(Текст);
	ЗаписьТекста.Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстСообщения(Знач Период)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПринятыеСообщенияССайта.ТекстСообщения
	|ИЗ
	|	РегистрСведений.ПринятыеСообщенияССайта КАК ПринятыеСообщенияССайта
	|ГДЕ
	|	ПринятыеСообщенияССайта.Период = &Период";
	Запрос.УстановитьПараметр("Период",Период);
	Выб = Запрос.Выполнить().Выбрать();
	Если Выб.Следующий() Тогда
		Возврат Выб.ТекстСообщения;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции
