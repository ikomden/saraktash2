&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьТоварыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	спрНоменклатура.Ссылка КАК Номенклатура,
	|	спрНоменклатура.Артикул КАК Артикул,
	|	спрНоменклатура.ОптовыйТовар КАК ОптовыйТовар,
	|	НоменклатураДополнительныеРеквизитыТипВязки.Значение КАК ТипВязки,
	|	НоменклатураДополнительныеРеквизитыЦвет.Значение КАК Цвет,
	|	НоменклатураДополнительныеРеквизитыРазмер.Значение КАК Размер
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	Справочник.Номенклатура КАК спрНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизитыТипВязки
	|		ПО спрНоменклатура.Ссылка = НоменклатураДополнительныеРеквизитыТипВязки.Ссылка
	|			И (НоменклатураДополнительныеРеквизитыТипВязки.Свойство = &Производство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизитыЦвет
	|		ПО спрНоменклатура.Ссылка = НоменклатураДополнительныеРеквизитыЦвет.Ссылка
	|			И (НоменклатураДополнительныеРеквизитыЦвет.Свойство = &Цвет)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизитыРазмер
	|		ПО спрНоменклатура.Ссылка = НоменклатураДополнительныеРеквизитыРазмер.Ссылка
	|			И (НоменклатураДополнительныеРеквизитыРазмер.Свойство = &Размер)
	|ГДЕ
	|	НЕ спрНоменклатура.ЭтоГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрНакопления.Запасы.Остатки(
	|			&Период,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						втНоменклатура.Номенклатура КАК втНоменклатура
	|					ИЗ
	|						втНоменклатура КАК втНоменклатура)
	|				И Склад = &Склад) КАК ЗапасыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втНоменклатураОпт.Номенклатура КАК НоменклатураОпт,
	|	втНоменклатураОпт.Артикул КАК АртикулОпт,
	|	втНоменклатура.Номенклатура КАК Номенклатура,
	|	втНоменклатура.Артикул КАК Артикул,
	|	ЕСТЬNULL(втОстаткиОпт.КоличествоОстаток, 0) КАК ОстатокОпт,
	|	ЕСТЬNULL(втОстаткиОпт.КоличествоОстаток, 0) КАК ИтогоОстатокОпт,
	|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0) КАК Остаток,
	|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0) КАК ИтогоОстаток
	|ИЗ
	|	втНоменклатура КАК втНоменклатураОпт
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстаткиОпт
	|		ПО втНоменклатураОпт.Номенклатура = втОстаткиОпт.Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНоменклатура КАК втНоменклатура
	|		ПО втНоменклатураОпт.Номенклатура <> втНоменклатура.Номенклатура
	|			И втНоменклатураОпт.ТипВязки = втНоменклатура.ТипВязки
	|			И втНоменклатураОпт.Цвет = втНоменклатура.Цвет
	|			И втНоменклатураОпт.Размер = втНоменклатура.Размер
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|		ПО (втНоменклатура.Номенклатура = втОстатки.Номенклатура)
	|ГДЕ
	|	втНоменклатураОпт.ОптовыйТовар
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	втНоменклатураОпт.Номенклатура,
	|	втНоменклатураОпт.Артикул,
	|	НоменклатураПохожиеРозничныеТовары.Номенклатура,
	|	НоменклатураПохожиеРозничныеТовары.Номенклатура.Артикул,
	|	ЕСТЬNULL(втОстаткиОпт.КоличествоОстаток, 0),
	|	ЕСТЬNULL(втОстаткиОпт.КоличествоОстаток, 0),
	|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0),
	|	ЕСТЬNULL(втОстатки.КоличествоОстаток, 0)
	|ИЗ
	|	втНоменклатура КАК втНоменклатураОпт
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстаткиОпт
	|		ПО втНоменклатураОпт.Номенклатура = втОстаткиОпт.Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ПохожиеРозничныеТовары КАК НоменклатураПохожиеРозничныеТовары
	|		ПО втНоменклатураОпт.Номенклатура = НоменклатураПохожиеРозничныеТовары.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|		ПО (НоменклатураПохожиеРозничныеТовары.Номенклатура = втОстатки.Номенклатура)
	|ГДЕ
	|	втНоменклатураОпт.ОптовыйТовар";
	
	Запрос.УстановитьПараметр("Склад",			Справочники.Склады.НайтиПоКоду("000000001"));
	Запрос.УстановитьПараметр("Производство",	ПланыВидовХарактеристик.ДополнительныеРеквизиты.НайтиПоКоду("000000003"));
	Запрос.УстановитьПараметр("Размер",			ПланыВидовХарактеристик.ДополнительныеРеквизиты.НайтиПоКоду("000000001"));
	Запрос.УстановитьПараметр("Цвет",			ПланыВидовХарактеристик.ДополнительныеРеквизиты.НайтиПоКоду("000000002"));
	Запрос.УстановитьПараметр("Период",			ТекущаяДата()+10);
	
	ЭтаФорма.Товары.Загрузить(Запрос.Выполнить().Выгрузить());	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТовары(Команда)
	
	Если Товары.Количество() <> 0 Тогда 
		
		Если Вопрос("Таблица товаров будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да Тогда
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьТоварыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНовыйОстатокОптПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//Проверка
	Если ТекущиеДанные.НовыйОстатокОпт > 0 Тогда //Добавить к опту
		
		Если ТекущиеДанные.НовыйОстатокОпт > ТекущиеДанные.Остаток Тогда
			ТекущиеДанные.НовыйОстатокОпт = 0;
			Сообщить("Не достаточно остатка!", СтатусСообщения.Важное);
		КонецЕсли;
		
	Иначе //Добавить к рознице
		
		Если ТекущиеДанные.НовыйОстатокОпт * -1 > ТекущиеДанные.ОстатокОпт Тогда
			ТекущиеДанные.НовыйОстатокОпт = 0;
			Сообщить("Не достаточно остатка!", СтатусСообщения.Важное);
		КонецЕсли;
		
	КонецЕсли;
	
	ТекущиеДанные.ИтогоОстатокОпт = ТекущиеДанные.ОстатокОпт + ТекущиеДанные.НовыйОстатокОпт;
	ТекущиеДанные.ИтогоОстаток = ТекущиеДанные.Остаток - ТекущиеДанные.НовыйОстатокОпт;
	
КонецПроцедуры


&НаСервере
Процедура СохранитьИзмененияНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тзТовары.НоменклатураОпт КАК НоменклатураОпт,
	|	тзТовары.Номенклатура КАК Номенклатура,
	|	тзТовары.НовыйОстатокОпт КАК НовыйОстатокОпт
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&тзТовары КАК тзТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТовары.НоменклатураОпт КАК Номенклатура,
	|	втТовары.НовыйОстатокОпт КАК Количество
	|ПОМЕСТИТЬ втПредв
	|ИЗ
	|	втТовары КАК втТовары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТовары.Номенклатура,
	|	-1 * втТовары.НовыйОстатокОпт
	|ИЗ
	|	втТовары КАК втТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПредв.Номенклатура КАК Номенклатура,
	|	СУММА(втПредв.Количество) КАК Количество
	|ИЗ
	|	втПредв КАК втПредв
	|
	|СГРУППИРОВАТЬ ПО
	|	втПредв.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(втПредв.Количество) <> 0";
	
	Запрос.УстановитьПараметр("тзТовары", Товары.Выгрузить(, "НоменклатураОпт, Номенклатура, НовыйОстатокОпт"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		
		Сообщить("Нет данных для изменения!", СтатусСообщения.Важное);
		
		Возврат;
		
	КонецЕсли;
	
	нДок = Документы.Корректировка.СоздатьДокумент();
	нДок.Дата = ТекущаяДата();
	
	Склад = Справочники.Склады.НайтиПоКоду("000000001");
	
	Пока Выборка.Следующий() Цикл
		
		нСтрока = нДок.Товары.Добавить();
		нСтрока.Склад = Склад;
		нСтрока.Номенклатура = Выборка.Номенклатура;
		нСтрока.Количество = Выборка.Количество;
		
	КонецЦикла;
	
	нДок.Записать(РежимЗаписиДокумента.Проведение);	
	
КонецПроцедуры


&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	Если Товары.Количество() <> 0 Тогда 
		
		Если Вопрос("Вы действительно хотите сохранить изменения остатков?", РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да Тогда
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СохранитьИзмененияНаСервере();
	
	Сообщить("Изменения сохранены!", СтатусСообщения.Информация);
	
	ЗаполнитьТоварыНаСервере();
	
КонецПроцедуры


&НаСервере
Процедура ПечатьНаклеекНаСервере(ТДПечать, МакетПечати)
	
	//Печать используем универсальную
	
	нДок = Документы.ПоступлениеТоваров.СоздатьДокумент();
	
	Для Каждого Строка Из Товары Цикл 
		
		Если Строка.НовыйОстатокОпт = 0 Тогда 
			
			Продолжить;
			
		КонецЕсли;
		
		нСтрока = нДок.Товары.Добавить();
		
		Если Строка.НовыйОстатокОпт > 0 Тогда
			
			нСтрока.Номенклатура = Строка.НоменклатураОпт;
			нСтрока.Количество = Строка.НовыйОстатокОпт;
			
		Иначе 
			
			нСтрока.Номенклатура = Строка.Номенклатура;
			нСтрока.Количество = Строка.НовыйОстатокОпт * - 1;
			
		КонецЕсли;
		
		ЦеныЗакупки = СлужебныйСервер.ЦеныЗакупки(нСтрока.Номенклатура, нДок.Поставщик, ТекущаяДата());
		ТекЦена = ЦеныЗакупки[нСтрока.Номенклатура];
		нСтрока.Цена = ?(ЗначениеЗаполнено(ТекЦена),ТекЦена,0);
		
	КонецЦикла;
	
	Если нДок.Товары.Количество() = 0 Тогда
		
		Сообщить("Нет изменений! Нечего печатать!");
		
	КонецЕсли;
	
	ТДПечатнаяФормаДокумента = Документы.ПоступлениеТоваров.ПолучитьПечатнуюФорму(нДок, МакетПечати);			
	ТДПечать.Вывести(ТДПечатнаяФормаДокумента);
	
КонецПроцедуры


&НаКлиенте
Процедура ПечатьНаклеек(Команда)
	
	МакетПечати = "ПечатьНаклеек";
	
	ТДПечать = Новый ТабличныйДокумент;
	ПечатьНаклеекНаСервере(ТДПечать, МакетПечати);
	ТДПечать.ОтображатьСетку = Ложь;
	ТДПечать.Защита = Ложь;
	ТДПечать.ТолькоПросмотр = Ложь;
	ТДПечать.ОтображатьЗаголовки = Ложь;
	ТДПечать.АвтоМасштаб = Истина;
	
	////Для наклеек свой принтер
	ТДПечать.ИмяПринтера = ПолныеПрава.ПолучитьЗначениеКонстанты("ИмяПринтераПечатьЭтикеток");
	ТДПечать.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТДПечать.ПолеСверху = 10;
	ТДПечать.ПолеСлева = 24;		
	ТДПечать.ПолеСнизу = 10;
	ТДПечать.ПолеСправа = 5;
	ТДПечать.Показать("Печать: Наклейки");
	
КонецПроцедуры

