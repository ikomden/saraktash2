Процедура УстановитьПараметрыСеанса() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Пользователи.Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.ИдентификаторПользователяИБ = &ИдентификаторПользователяИБ";
	
	ТекущийПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	Запрос.УстановитьПараметр("ИдентификаторПользователяИБ",ТекущийПользовательИБ.УникальныйИдентификатор);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПараметрыСеанса.ТекущийПользователь = Выборка.Ссылка;
	Иначе
		// создать нового пользователя
		НовыйПользователь = Справочники.Пользователи.СоздатьЭлемент();
		НовыйПользователь.Код = ТекущийПользовательИБ.Имя;
		НовыйПользователь.Наименование = ТекущийПользовательИБ.ПолноеИмя;
		НовыйПользователь.ИдентификаторПользователяИБ = ТекущийПользовательИБ.УникальныйИдентификатор;
		НовыйПользователь.Записать();
		ПараметрыСеанса.ТекущийПользователь = НовыйПользователь.Ссылка;
	КонецЕсли;
	
	ПараметрыСеанса.РабочееМестоКлиента = Справочники.РабочиеМеста.ПустаяСсылка();
	
КонецПроцедуры

Функция ПолучитьЗначениеКонстанты(ИмяКонстанты) Экспорт 
	
	Возврат Константы[ИмяКонстанты].Получить();
	
КонецФункции

Функция ПолучитьПеречисление(ПеречислениеИмя, ПеречислениеЗначение) Экспорт 
	
	Возврат Перечисления[ПеречислениеИмя][ПеречислениеЗначение];
	
КонецФункции